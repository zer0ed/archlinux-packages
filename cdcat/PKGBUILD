# Contributor: Wes Brewer <brewerw@gmail.com>
pkgname=cdcat
pkgver=1.01b
pkgrel=6
pkgdesc="CD/DVD/Media catalog software (QT based)"
arch=('i686' 'x86_64')
url="http://cdcat.sourceforge.net/"
license=('GPL')
depends=('pcre' 'qt3')
source=(http://cdcat.sourceforge.net/$pkgname-$pkgver.tar.bz2
        gcc4_1.patch
        mp3tag_parser.patch
        cdcat.desktop)
md5sums=('59b321ff3848b34cb6862fd2a408cb44'
         '5a4f73ca16513f4b902b61ce79fc0f14'
         '933773c216e42b838cafa25993389aec'
         '1ad7d21d77dd4f9d0b56216fe54f61ce')

build() {
  cd $srcdir/CdCat-$pkgver/src

  # GCC 4.1 patch
  patch -Np2 -i "$srcdir/gcc4_1.patch" || return 1
  # mp3tag parser patch
  patch -Np2 -i "$srcdir/mp3tag_parser.patch" || return 1
  # fix path from /usr/local to /usr
  sed -i 's|/usr/local|/usr|' cdcat.pro
  
  # set qt3 profile before using qmake
  source /etc/profile.d/qt3.sh
  export QMAKESPEC=/opt/qt/mkspecs/linux-g++

  ${QTDIR}/bin/qmake cdcat.pro || return 1
  make || return 1
  make INSTALL_ROOT=$startdir/pkg install || return 1

  # install menu link, pixmap icon, and mimetype
  cd $startdir/src/CdCat-$pkgver
  install -Dm644 ../cdcat.desktop $startdir/pkg/usr/share/applications/cdcat.desktop
  install -Dm644 cdcat.png $startdir/pkg/usr/share/pixmaps/cdcat.png
  install -Dm644 KDE/x-hcf.xml $startdir/pkg/usr/share/mime/packages/x-hcf.xml
}

# vim:set ts=2 sw=2 et:
